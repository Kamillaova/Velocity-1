plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.0' apply false
    id 'org.cadixdev.licenser' version '0.6.1' apply false
}

allprojects {
    group 'com.velocitypowered'
    version '3.1.2-SNAPSHOT'

    ext {
        // dependency versions
        adventureVersion = '4.11.0'
        junitVersion = '5.8.2'
        slf4jVersion = '1.7.36'
        log4jVersion = '2.17.2'
        nettyVersion = '4.1.78.Final'
        guavaVersion = '31.1-jre'
        checkerFrameworkVersion = '3.22.2'
        configurateVersion = '4.1.2'

        getCurrentShortRevision = {
            new ByteArrayOutputStream().withStream { os ->
                exec {
                    executable = "git"
                    args = ["rev-parse", "HEAD"]
                    standardOutput = os
                }
                return os.toString().trim().substring(0, 8)
            }
        }
    }

    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(17))
    }

    repositories {
        mavenCentral()

        // for kyoripowered dependencies
        maven {
            url 'https://oss.sonatype.org/content/groups/public/'
        }

        // Velocity repo
        maven {
            url "https://nexus.velocitypowered.com/repository/maven-public/"
        }
    }

    test {
        reports {
            junitXml.required = true
        }
    }
}
